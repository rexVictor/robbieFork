plugins {
    id "org.sonarqube" version "1.0"
}



allprojects {
    repositories {
        mavenCentral()
    }
    sonarqube {
        properties {
            property "sonar.jdbc.username", "sonarqube"
            property "sonar.jdbc.password", "sonarqube"
            property "sonar.jdbc.url","jdbc:postgresql://rexis-server/sonarqube"
            property "sonar.jacoco.reportPath", "$buildDir/jacoco/jacocoTest.exec"
            property "sonar.host.url", "http://regina:9000"
            //property "sonar.analysis.mode", "incremental"
        }
    }
}


subprojects {
    apply plugin : 'java'
    apply plugin : 'checkstyle'
    apply plugin : 'findbugs'
    apply plugin : 'jacoco'

    dependencies {
        testCompile "org.testng:testng:+"
        compile "org.slf4j:slf4j-api:+"
        compile "ch.qos.logback:logback-classic:+"
    }
    test {
        useTestNG() {
        }
        jacoco {
            destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        }
        testLogging {
            showStandardStreams = true
        }
    }

    checkstyle {
        toolVersion = "6.8"
        configFile = new File(rootDir, "config/checkstyle/checkstyle.xml")
    }

    findbugs {
        effort = "max"
        reportLevel = "low"
    }

}

task docs(type: Javadoc) {
    source subprojects.collect {project -> project.sourceSets.main.allJava }
    destinationDir = new File(buildDir,'doc')
    // Might need a classpath
    classpath = files(subprojects.collect {project ->
        project.sourceSets.main.compileClasspath})
}

project(':testes') {
    dependencies {
        testCompile project(':testhelp')
    }
}

project(':api') {
    dependencies {
        testCompile project(':testhelp')
    }
}

project(':model') {
    dependencies {
        compile project(':api')
        testCompile project(':testhelp')
    }
}

project(':controllers') {
    dependencies {
        compile project(':api')
        testCompile project(':testes')
        testCompile project(':testhelp')
    }
}
// vim:set shiftwidth=4 softtabstop=4 expandtab:
